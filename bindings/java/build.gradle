// EVMC: Ethereum Client-VM Connector API.
// Copyright 2019-2020 The EVMC Authors.
// Licensed under the Apache License, Version 2.0.

plugins {
    id 'com.diffplug.gradle.spotless' version '3.29.0'
}


apply from: "${rootDir}/wrapper.gradle"
allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = '1.11'
    targetCompatibility = '1.11'
    
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        java {
        // This path needs to be relative to each project
        target fileTree('.') {
            include '**/*.java'
            exclude '**/.gradle/**'
        }
        importOrder 'org.ethereum', 'java', ''
        trimTrailingWhitespace()
        endWithNewline()
        googleJavaFormat('1.7')
        }
    }
}

subprojects {

    tasks.withType(Test) {
        testLogging.showStandardStreams = true
        // If GRADLE_MAX_TEST_FORKS is not set, use half the available processors
        maxParallelForks = (System.getenv('GRADLE_MAX_TEST_FORKS') ?: (Runtime.runtime.availableProcessors().intdiv(2) ?: 1)).toInteger()
        useJUnitPlatform()
        reports {
            junitXml.enabled = true
        }
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                groupId 'org.ethereum'
                artifactId 'evmc'
                version '7.4.0'
            }
        }
    }
}

